#version 330 core
precision highp float;

in OBJECT_OUT {
    vec2 BaseUV;
    vec4 UV;
    vec4 Extra; // x=Type, y=SortId, z=Shade, w=Alpha
    vec4 Color;
    vec4 Mask1;
    vec4 Mask2;
} vsInput;

out vec4 FragColor;

uniform sampler2D GameTexture;
uniform float PixelRange;
uniform vec2 TextTextureSize;
uniform sampler2D TextTexture;
uniform float Zoom;
uniform int RenderPass; // 0 = opaque, 1 = transparent

const int OpaquePass = 0;
const int OutlineGlowPass = 1;

const float TypeGameObject = 0.0;
const float TypeText = 3.0;
const float TypeBar = 4.0;
const float TypeEffect = 5.0;

vec2 map(vec2 base, vec2 uvMin, vec2 uvMax) {
    return vec2(base.x * (uvMax.x - uvMin.x) + uvMin.x, base.y * (uvMax.y - uvMin.y) + uvMin.y);
}

float median(float a, float b, float c) {
    return max(min(a, b), min(max(a, b), c));
}

float samp(vec2 uv, vec2 dx, vec2 dy) {
    return textureLod(GameTexture, uv, 0.0).a;
}

bool inBounds(vec2 uv, vec2 minUV, vec2 maxUV){
    if (uv.x < minUV.x ||
    uv.x > maxUV.x ||
    uv.y < minUV.y ||
    uv.y > maxUV.y)
    {
        return false;
    }
    return true;
}

vec4 GetGameObject() {
    const float INV_TEX_SIZE = 1.0 / 4096.0;

    // uvMax precomputed once, reused in map() and as loop bounds
    vec2 uvMax = vsInput.UV.xy + vsInput.UV.zw;
    vec2 uv = map(vsInput.BaseUV, vsInput.UV.xy, uvMax);
    vec2 dx = dFdx(uv);
    vec2 dy = dFdy(uv);
    // every atlas here only has 1 mip level so textureGrad's LOD select was
    // pointless - just a buggy path on old Intel drivers. dx/dy still used below.
    vec4 color = textureLod(GameTexture, uv, 0.0);
    color.rgb -= vsInput.Extra.z * 0.241 * clamp(vsInput.BaseUV.y - 0.4, 0.0, 0.4);
    if (RenderPass == OpaquePass){
        if (color.a < 1.0 || vsInput.Extra.w < 1.0){
            discard;
        }
        return color;
    }

    if (color.a >= 1.0){
        if (vsInput.Extra.w < 1.0){
            return color;
        }
        discard;
    }

    float pxW = length(dx);
    float pxH = length(dy);
    vec2 invPx = vec2(1.0 / pxW, 1.0 / pxH);

    // Reuses invPx instead of recomputing length(dx * 4096.0) from scratch
    float pixelsInOneTexel = max(invPx.x, invPx.y) * INV_TEX_SIZE;
    float outlineSize = floor(max(1.0, Zoom));
    float glowSize = max(6.0, pixelsInOneTexel);

    ivec2 currentTexel = ivec2(uv * 4096.0);
    vec2  minUV = vsInput.UV.xy;

    vec2 dirs[8] = vec2[](
    -dx - dy, -dy, dx - dy,  dx,
    dx + dy,  dy, -dx + dy, -dx
    );

    bool belowTexel = (uvMax.y - uv.y + outlineSize * pxH) * 4096.0 < 1.0;

    bool foundOutline = false;
    float nearestDist = 999.0;
    int glowSizeInt = int(glowSize);
    int outlineSizeInt = int(outlineSize);

    int stepSize = int(ceil(Zoom * 2));
    for (int i = 1; i <= glowSizeInt && !foundOutline; i += stepSize) {
        if (i > outlineSizeInt && belowTexel) {
            discard;
        }

        float fi = float(i);
        for (int j = 0; j < 8; j++) {
            vec2 sampleUV = uv + dirs[j] * fi;
            if (!inBounds(sampleUV, minUV, uvMax)){
                continue;
            }

            ivec2 neighborTexel = ivec2(sampleUV * 4096.0);
            if (neighborTexel == currentTexel){
                continue;
            }
            
            if (texelFetch(GameTexture, neighborTexel, 0).a == 0.0){
                continue;
            }

            vec2 nearestPoint = clamp(uv,
            vec2(neighborTexel)            * INV_TEX_SIZE,
            vec2(neighborTexel + ivec2(1)) * INV_TEX_SIZE);
            vec2 distPx = abs(uv - nearestPoint) * invPx;

            if (max(distPx.x, distPx.y) <= outlineSize) {
                foundOutline = true;
                break;
            }

            nearestDist = min(nearestDist, length(distPx));
        }
    }

    if (foundOutline){
        return vec4(vsInput.Color.rgb, 1.0);
    }

    if (nearestDist < 999.0) {
        float normalized = nearestDist / glowSize;
        float glowAlpha  = 0.8 * exp(-normalized * 4.0) * (1.0 - smoothstep(0.8, 1.0, normalized));
        if (glowAlpha > 0.0){
            return vec4(vsInput.Color.rgb, glowAlpha);
        }
    }

    discard;
}

vec4 GetText() {
    vec2 uv = map(vsInput.BaseUV, vsInput.UV.xy, vsInput.UV.xy + vsInput.UV.zw);
    vec3 samp = texture(TextTexture, uv).rgb;
    float pRange = PixelRange;
    vec2 dim = TextTextureSize;

    vec2 msdfUnit = pRange / dim;
    float sigDist = median(samp.r, samp.g, samp.b) - 0.5f;
    sigDist = sigDist * dot(msdfUnit, 0.5f / fwidth(uv));
    const float strokeThickness = 0.250f * 0.75f;
    float strokeDist = median(samp.r, samp.g, samp.b) - 0.25f * (1.0 + (pRange - 12) / pRange) - strokeThickness;
    strokeDist = -(abs(strokeDist) - strokeThickness);
    strokeDist = strokeDist * dot(msdfUnit, 0.5f / fwidth(uv));
    float opacity = clamp(sigDist + 0.5f, 0.0f, 1.0f);
    float strokeOpacity = clamp(strokeDist + 0.5f, 0.0f, 1.0f);
    return mix(vec4(0, 0, 0, 1), vsInput.Color, opacity) * max(opacity, strokeOpacity);
}

void main() {
    vec4 outputColor;
    float id = vsInput.Extra.x;

    if (id == TypeGameObject || id == TypeEffect) {
        outputColor = GetGameObject();
    } else if (id == TypeText) {
        outputColor = GetText();
    } else if (id == TypeBar) {
        outputColor = vsInput.Color;
    } else {
        outputColor = vec4(0, 0, 0, 0);
    }

    outputColor.a *= vsInput.Extra.w;
    if (outputColor.a == 0) {
        discard;
    }

    FragColor = outputColor;
}